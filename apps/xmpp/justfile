# XMPP Service Justfile
# Profile-based Docker Compose orchestration

set export := true

# XMPP Service Justfile
# Component-specific utilities
# Use root 'just' for lifecycle management (up, down, logs)

# Open shell in running Prosody container
[group('Management')]
shell:
    @CONTAINER=$(docker compose -p atl-chat ps -q atl-xmpp-server 2>/dev/null); \
    if [ -n "$CONTAINER" ]; then \
        docker exec -it $CONTAINER /bin/bash; \
    else \
        echo "XMPP server not running (or project name mismatch)"; \
    fi

# Reload Prosody configuration
[group('Management')]
reload:
    @echo "Reloading Prosody configuration..."
    @CONTAINER=$(docker compose -p atl-chat ps -q atl-xmpp-server 2>/dev/null); \
    if [ -n "$CONTAINER" ]; then \
        docker exec $CONTAINER prosodyctl reload; \
    else \
        echo "XMPP server not running"; \
    fi

# Add a user (usage: just xmpp adduser user@domain.com [password])
[group('User Management')]
adduser user password="":
    @echo "Adding user: $user"
    @CONTAINER=$(docker compose -p atl-chat ps -q atl-xmpp-server 2>/dev/null); \
    if [ -n "$CONTAINER" ]; then \
        if [ -n "$password" ]; then \
            docker exec $CONTAINER bash -c "echo -e '$password\n$password' | prosodyctl adduser $user"; \
        else \
            docker exec -it $CONTAINER prosodyctl adduser $user; \
        fi; \
    else \
        echo "XMPP server not running"; \
    fi

# Delete a user (usage: just xmpp deluser user@domain.com)
[group('User Management')]
deluser user:
    @echo "Deleting user: $user"
    @CONTAINER=$(docker compose -p atl-chat ps -q atl-xmpp-server 2>/dev/null); \
    if [ -n "$CONTAINER" ]; then \
        docker exec $CONTAINER prosodyctl deluser $user; \
    else \
        echo "XMPP server not running"; \
    fi

# Backup PostgreSQL database
[group('Database')]
db-backup:
    @echo "Backing up database..."
    @CONTAINER=$(docker compose -p atl-chat ps -q atl-xmpp-db 2>/dev/null); \
    if [ -n "$CONTAINER" ]; then \
        BACKUP_FILE="backup_$(date +%Y%m%d_%H%M%S).sql"; \
        docker exec $CONTAINER pg_dumpall -U prosody > $BACKUP_FILE; \
        echo "Database backup saved to: $BACKUP_FILE"; \
    else \
        echo "Database not running"; \
    fi

# Setup Prosody community modules locally
[group('Modules')]
setup-modules:
    @echo "Setting up Prosody community modules locally..."
    ./services/prosody/scripts/setup-modules-locally.sh

# Update local Prosody community modules
[group('Modules')]
update-modules:
    @echo "Updating local Prosody community modules..."
    @if [[ -d "prosody-modules/.hg" ]]; then \
        (cd prosody-modules && (hg pull bundle 2>/dev/null || hg pull default) && hg update); \
        echo "Modules updated successfully"; \
    else \
        echo "prosody-modules repository not found. Run 'just xmpp-setup-modules' first"; \
    fi

# Check certificate status (usage: just xmpp check-cert domain)
[group('Certificates')]
check-cert domain:
    @echo "Checking certificate for: $domain"
    @CONTAINER=$(docker compose -p atl-chat ps -q atl-xmpp-server 2>/dev/null); \
    if [ -n "$CONTAINER" ]; then \
        docker exec $CONTAINER /usr/local/bin/certificate-monitor.sh check "$domain"; \
    else \
        echo "XMPP server not running"; \
    fi
